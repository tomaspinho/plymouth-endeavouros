# This is an example plymouth plugin script

Window.SetBackgroundTopColor(0, 0, 0);
Window.SetBackgroundBottomColor(0, 0, 0);

// EndeavourOS color #7f7fff = rgb(127, 127, 255)
text_color.r = 127/255;
text_color.g = 127/255;
text_color.b = 255/255;

max_prompt_message_width = 40;

background.image = Image("wallpaper.png");
background.aspect_ratio = background.image.GetWidth() / background.image.GetHeight();
background.window_ratio = Window.GetWidth() / background.image.GetWidth();
background.image = background.image.Scale(Window.GetWidth(), background.image.GetHeight() * background.window_ratio);
background.sprite = Sprite(background.image);
background.sprite.SetX (Window.GetX());
background.sprite.SetY (Window.GetY() + (Window.GetHeight()-background.image.GetHeight()) / 2);

caps_sprite = Sprite();
caps_sprite.SetPosition(10, 10, 10000);

fun refresh_callback ()
  {
    caps_state = Plymouth.GetCapslockState();
    if (global.dialog) { //&& global.dialog.capslock_icon && global.dialog.capslock_icon.sprite) {
      if (caps_state) {
        global.dialog.capslock_icon.sprite.SetOpacity (1);
      } else {
        global.dialog.capslock_icon.sprite.SetOpacity (0);
      }
    }
  }
  
Plymouth.SetRefreshFunction (refresh_callback);

#----------------------------------------- Dialogue --------------------------------

status = "normal";
fun dialog_setup(message)
  {
    local.box;
    local.entry;
    local.prompt;
    local.capslock_icon;

    box.image = Image("box.png");
    entry.image = Image("entry.png");

    cur_message = "";
    cur_index = 0;
    for (index = 0; index < String(message).Length(); index++)
      {
        cur_message = cur_message + String(message).CharAt(index);
        if (String(cur_message).Length() == max_prompt_message_width) {
          prompt[cur_index].image = Image.Text (cur_message, 1, 1, 1);

          prompt[cur_index].sprite = Sprite(prompt[cur_index].image);

          cur_message = "";
          cur_index++;
        }
      }

    // Get the remainder string
    prompt[cur_index].image = Image.Text (cur_message, 1, 1, 1);
    prompt[cur_index].sprite = Sprite(prompt[cur_index].image);
    cur_index++;

    for (index = 0; prompt[index]; index++)
      {
        prompt[index].x = Window.GetX() + Window.GetWidth()  / 2 - prompt[index].image.GetWidth ()/2;
        prompt[index].y = Window.GetY() + Window.GetHeight() * (5/6) - (cur_index - index)*prompt[index].image.GetHeight() - 8;
        prompt[index].z = 10000;
        prompt[index].sprite.SetPosition(prompt[index].x, prompt[index].y, prompt[index].z);
      }

    capslock_icon.image = Image("capslock-icon.png");

    box.sprite = Sprite(box.image);
    box.x = Window.GetX() + Window.GetWidth()  / 2 - box.image.GetWidth ()/2;
    box.y = Window.GetY() + Window.GetHeight() * (5/6);
    box.z = 10000;
    box.sprite.SetPosition(box.x, box.y, box.z);
    
    entry.sprite = Sprite(entry.image);
    entry.x = box.x + box.image.GetWidth()/2 - entry.image.GetWidth() / 2;
    entry.y = box.y + box.image.GetHeight()/2 - entry.image.GetHeight()/2;
    entry.z = box.z + 1;
    entry.sprite.SetPosition(entry.x, entry.y, entry.z);

    capslock_icon.sprite = Sprite(capslock_icon.image);
    capslock_icon.x = entry.x + entry.image.GetWidth() - capslock_icon.image.GetWidth() - 8;
    capslock_icon.y = entry.y + (entry.image.GetHeight() - capslock_icon.image.GetHeight())/2;
    capslock_icon.z = entry.z;
    capslock_icon.sprite.SetPosition(capslock_icon.x, capslock_icon.y, capslock_icon.z);
    capslock_icon.sprite.SetOpacity(0);

    // Get the progress percentage out of the way
    set_progress_position();

    global.dialog.box = box;
    global.dialog.entry = entry;
    global.dialog.capslock_icon = capslock_icon;
    global.dialog.prompt = prompt;
    global.dialog.bullet_image = Image("bullet.png");
    dialog_opacity (1);
  }
    
fun dialog_opacity(opacity)
  {
    dialog.box.sprite.SetOpacity (opacity);
    dialog.entry.sprite.SetOpacity (opacity);
    for (index = 0; dialog.prompt[index]; index++)
      {
        dialog.prompt[index].sprite.SetOpacity(opacity);
      }
    for (index = 0; dialog.bullet[index]; index++)
      {
        dialog.bullet[index].sprite.SetOpacity(opacity);
      }
  }

fun display_normal_callback ()
  {
    global.status = "normal";
    if (global.dialog)
      dialog_opacity (0);
    // Move the progress percentage back to the centre
    set_progress_position();
  }

fun display_password_callback (prompt, bullets)
  {
    global.status = "password";
    if (!global.dialog)
	dialog_setup(prompt);
    else
	dialog_opacity(1);
    for (index = 0; dialog.bullet[index] || index < bullets; index++)
      {
        if (!dialog.bullet[index] && index < 22)
          {
            dialog.bullet[index].sprite = Sprite(dialog.bullet_image);
            dialog.bullet[index].x = dialog.entry.x + 4 + index * (dialog.bullet_image.GetWidth() + 2);
            dialog.bullet[index].y = dialog.entry.y + dialog.entry.image.GetHeight() / 2 - dialog.bullet_image.GetHeight() / 2;
            dialog.bullet[index].z = dialog.entry.z + 1;
            dialog.bullet[index].sprite.SetPosition(dialog.bullet[index].x, dialog.bullet[index].y, dialog.bullet[index].z);
          }
        if (index < bullets)
          dialog.bullet[index].sprite.SetOpacity(1);
        else
          dialog.bullet[index].sprite.SetOpacity(0);
      }
  }

Plymouth.SetDisplayNormalFunction(display_normal_callback);
Plymouth.SetDisplayPasswordFunction(display_password_callback);

#----------------------------------------- Progress Percentage --------------------------------

progress_text.sprite = Sprite();

fun progress_callback (duration, progress)
  {
    progress_txt = Math.Int(progress * 100) + "";

    for(i = 0; i < 4 - String(progress_txt).Length(); i++) {
      progress_txt = " " + progress_txt;
    }

    progress_txt = progress_txt + "%";

    progress_text.image = Image.Text(progress_txt, text_color.r, text_color.g, text_color.b, 1, "Sans 32");
    progress_text.sprite.SetImage(progress_text.image);
    set_progress_position();
  }

Plymouth.SetBootProgressFunction(progress_callback);

fun set_progress_position()
  {
    if (global.status == "password")
      progress_text.sprite.SetPosition(Window.GetX() + Window.GetWidth() / 2 + dialog.box.image.GetWidth () / 2 + 84 - progress_text.image.GetWidth(), Window.GetY() + Window.GetHeight() * 5/6 + dialog.box.image.GetHeight()/2 - progress_text.image.GetHeight()/2, 1);
    else
      progress_text.sprite.SetPosition(Window.GetX() + Window.GetWidth()  / 2 - progress_text.image.GetWidth() /2, Window.GetY() + Window.GetHeight() * 5/6, 1);
  }

#----------------------------------------- Quit --------------------------------

fun quit_callback ()
{
  logo.sprite.SetOpacity (1);
}

Plymouth.SetQuitFunction(quit_callback);

#----------------------------------------- Message --------------------------------

message_sprite = Sprite();
message_sprite.SetPosition(10, 10, 10000);

fun display_message_callback (text)
{
  my_image = Image.Text(text, 1, 1, 1);
  message_sprite.SetImage(my_image);
}

fun hide_message_callback (text)
{
  message_sprite = Sprite();
  message_sprite.SetPosition(10, 10, 10000);
}

Plymouth.SetDisplayMessageFunction (display_message_callback);
Plymouth.SetHideMessageFunction (hide_message_callback);
