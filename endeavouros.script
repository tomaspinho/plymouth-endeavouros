# This is an example plymouth plugin script

Window.SetBackgroundTopColor(0, 0, 0);
Window.SetBackgroundBottomColor(0, 0, 0);

/*
logo.image = Image("endeavouros-dark.png");
logo.sprite = Sprite(logo.image);
logo.opacity_angle = 0;
*/

// EndeavourOS color #7f7fff = rgb(127, 127, 255)
text_color.r = 127/255;
text_color.g = 127/255;
text_color.b = 255/255;

background.image = Image("wallpaper.png");
background.aspect_ratio = background.image.GetWidth() / background.image.GetHeight();
background.window_ratio = Window.GetWidth() / background.image.GetWidth();
background.image = background.image.Scale(Window.GetWidth(), background.image.GetHeight() * background.window_ratio);
background.sprite = Sprite(background.image);
background.sprite.SetX (Window.GetX());
background.sprite.SetY (Window.GetY() + (Window.GetHeight()-background.image.GetHeight()) / 2);

caps_sprite = Sprite();
caps_sprite.SetPosition(10, 10, 10000);

fun refresh_callback ()
  {
    /*
    logo.opacity_angle += ((2 * 3.14) / 50) * 0.5;  # 0.5 HZ
    min_opacity = 0.3;
    opacity = (Math.Cos(logo.opacity_angle) + 1) / 2;
    opacity *= 1 - min_opacity;
    opacity += min_opacity;
    logo.sprite.SetX (Window.GetX() + Window.GetWidth()  / 2 - logo.image.GetWidth()  / 2);
    logo.sprite.SetY (Window.GetY() + Window.GetHeight() / 2 - logo.image.GetHeight() / 2);
    logo.sprite.SetOpacity (opacity);
    */

    caps_state = Plymouth.GetCapslockState();
    if (global.dialog) { //&& global.dialog.capslock_icon && global.dialog.capslock_icon.sprite) {
      if (caps_state) {
        global.dialog.capslock_icon.sprite.SetOpacity (1);
      } else {
        global.dialog.capslock_icon.sprite.SetOpacity (0);
      }
    }
  }
  
Plymouth.SetRefreshFunction (refresh_callback);

#----------------------------------------- Dialogue --------------------------------

status = "normal";
fun dialog_setup(message)
  {
    local.box;
    local.entry;
    local.prompt;
    local.capslock_icon;

    box.image = Image("box.png");
    entry.image = Image("entry.png");
    prompt.image = Image.Text (message, 1, 1, 1);
    capslock_icon.image = Image("capslock-icon.png");

    prompt.sprite = Sprite(prompt.image);
    prompt.x = Window.GetX() + Window.GetWidth()  / 2 - prompt.image.GetWidth ()/2;
    prompt.y = Window.GetY() + Window.GetHeight() * (5/6) - prompt.image.GetHeight() - 8;
    prompt.z = 10000;
    prompt.sprite.SetPosition(prompt.x, prompt.y, prompt.z);

    box.sprite = Sprite(box.image);
    box.x = Window.GetX() + Window.GetWidth()  / 2 - box.image.GetWidth ()/2;
    box.y = Window.GetY() + Window.GetHeight() * (5/6);
    box.z = 10000;
    box.sprite.SetPosition(box.x, box.y, box.z);
    
    entry.sprite = Sprite(entry.image);
    entry.x = box.x + box.image.GetWidth()/2 - entry.image.GetWidth() / 2;
    entry.y = box.y + box.image.GetHeight()/2 - entry.image.GetHeight()/2;
    entry.z = box.z + 1;
    entry.sprite.SetPosition(entry.x, entry.y, entry.z);

    capslock_icon.sprite = Sprite(capslock_icon.image);
    capslock_icon.x = entry.x + entry.image.GetWidth() - capslock_icon.image.GetWidth() - 8;
    capslock_icon.y = entry.y + (entry.image.GetHeight() - capslock_icon.image.GetHeight())/2;
    capslock_icon.z = entry.z;
    capslock_icon.sprite.SetPosition(capslock_icon.x, capslock_icon.y, capslock_icon.z);
    capslock_icon.sprite.SetOpacity(0);

    // Get the progress percentage out of the way
    set_progress_position();

    global.dialog.box = box;
    global.dialog.entry = entry;
    global.dialog.capslock_icon = capslock_icon;
    global.dialog.prompt = prompt;
    global.dialog.bullet_image = Image("bullet.png");
    dialog_opacity (1);
  }
    
fun dialog_opacity(opacity)
  {
    dialog.box.sprite.SetOpacity (opacity);
    dialog.entry.sprite.SetOpacity (opacity);
    dialog.prompt.sprite.SetOpacity (opacity);
    for (index = 0; dialog.bullet[index]; index++)
      {
        dialog.bullet[index].sprite.SetOpacity(opacity);
      }
  }

fun display_normal_callback ()
  {
    global.status = "normal";
    if (global.dialog)
      dialog_opacity (0);
    // Move the progress percentage back to the centre
    set_progress_position();
  }

fun display_password_callback (prompt, bullets)
  {
    global.status = "password";
    if (!global.dialog)
	dialog_setup(prompt);
    else
	dialog_opacity(1);
    for (index = 0; dialog.bullet[index] || index < bullets; index++)
      {
        if (!dialog.bullet[index] && index < 22)
          {
            dialog.bullet[index].sprite = Sprite(dialog.bullet_image);
            dialog.bullet[index].x = dialog.entry.x + 4 + index * (dialog.bullet_image.GetWidth() + 2);
            dialog.bullet[index].y = dialog.entry.y + dialog.entry.image.GetHeight() / 2 - dialog.bullet_image.GetHeight() / 2;
            dialog.bullet[index].z = dialog.entry.z + 1;
            dialog.bullet[index].sprite.SetPosition(dialog.bullet[index].x, dialog.bullet[index].y, dialog.bullet[index].z);
          }
        if (index < bullets)
          dialog.bullet[index].sprite.SetOpacity(1);
        else
          dialog.bullet[index].sprite.SetOpacity(0);
      }
  }

Plymouth.SetDisplayNormalFunction(display_normal_callback);
Plymouth.SetDisplayPasswordFunction(display_password_callback);

#----------------------------------------- Progress Bar --------------------------------

/*
progress_box.image = Image("progress_box.png");
progress_box.sprite = Sprite(progress_box.image);

progress_box.x = Window.GetX() + Window.GetWidth() / 2 - progress_box.image.GetWidth() / 2;
progress_box.y = Window.GetY() + Window.GetHeight() * 0.75 - progress_box.image.GetHeight() / 2;
progress_box.sprite.SetPosition(progress_box.x, progress_box.y, 0);

progress_bar.original_image = Image("progress_bar.png");
progress_bar.sprite = Sprite();

progress_bar.x = Window.GetX() + Window.GetWidth()  / 2 -        progress_bar.original_image.GetWidth() / 2;
progress_bar.y = Window.GetY() + Window.GetHeight() / 2 * 1.5  - progress_box.image.GetHeight() / 2 + (progress_box.image.GetHeight() - progress_bar.original_image.GetHeight()) / 2;
progress_bar.sprite.SetPosition(progress_bar.x, progress_bar.y, 1);
*/

progress_text.sprite = Sprite();

fun progress_callback (duration, progress)
  {
    progress_txt = Math.Int(progress * 100) + "";

    for(i = 0; i < 4 - String(progress_txt).Length(); i++) {
      progress_txt = " " + progress_txt;
    }

    progress_txt = progress_txt + "%";

    progress_text.image = Image.Text(progress_txt, text_color.r, text_color.g, text_color.b, 1, "Sans 32");
    progress_text.sprite.SetImage(progress_text.image);
    set_progress_position();
  }

Plymouth.SetBootProgressFunction(progress_callback);

fun set_progress_position()
  {
    if (global.status == "password")
      progress_text.sprite.SetPosition(Window.GetX() + Window.GetWidth() / 2 + dialog.box.image.GetWidth () / 2 + 80 - progress_text.image.GetWidth(), Window.GetY() + Window.GetHeight() * 5/6 + dialog.box.image.GetHeight()/2 - progress_text.image.GetHeight()/2, 1);
    else
      progress_text.sprite.SetPosition(Window.GetX() + Window.GetWidth()  / 2 - progress_text.image.GetWidth() /2, Window.GetY() + Window.GetHeight() * 5/6, 1);
  }

#----------------------------------------- Quit --------------------------------

fun quit_callback ()
{
  logo.sprite.SetOpacity (1);
}

Plymouth.SetQuitFunction(quit_callback);

#----------------------------------------- Message --------------------------------

message_sprite = Sprite();
message_sprite.SetPosition(10, 10, 10000);

fun display_message_callback (text)
{
  my_image = Image.Text(text, 1, 1, 1);
  message_sprite.SetImage(my_image);
}

fun hide_message_callback (text)
{
  message_sprite = Sprite();
  message_sprite.SetPosition(10, 10, 10000);
}

Plymouth.SetDisplayMessageFunction (display_message_callback);
Plymouth.SetHideMessageFunction (hide_message_callback);
